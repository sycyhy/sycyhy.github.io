buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/artifactory/repo" }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'org.jbake:jbake-core:2.6.1'
    classpath 'org.asciidoctor:asciidoctorj:1.5.5'
  }
}

plugins {
  id 'org.jbake.site' version '1.4.0'
  id "org.ajoberstar.git-publish" version "1.0.1"
  id "com.eriwen.gradle.js" version "2.14.1"
}

apply plugin: 'idea'
apply plugin: 'groovy'

jbake {
  clearCache = true
  groovyTemplatesVersion = '2.4.15'
  asciidoctorJavaIntegrationVersion = '0.1.4'
  asciidoctorjVersion = '1.5.5'
}

gitPublish {
  repoUri = "git@github.com:sycyhy/sycyhy.github.io.git"
  branch = "master"
  contents {
    from "$projectDir/build/jbake"
    into "."
  }
  commitMessage = 'Publishing a new blog/blog changes via git-publish plugin'
}

gitPublishCopy.dependsOn(bake)

javascript.source {
    dev {
        js {
            srcDir "$projectDir/src/jbake/assets/js"
            include "*.js"
            exclude "*.min.js"
        }
    }
    prod {
        js {
            srcDir "$projectDir/src/jbake/assets/js"
            include "*.min.js"
        }
    }
}

combineJs {
    encoding = "UTF-8"
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/all.js")
}


task copyCombinedSources(type: Copy) {
    from "$buildDir/all-min.js"
    into "$buildDir/jbake/js/"
}

task deleteFiles(type: Delete) {
    delete fileTree('build/jbake/js/') {
        include '**/*.js'
    }
}

minifyJs {
    source = combineJs
    dest = file("${buildDir}/all-min.js")
    sourceMap = file("${buildDir}/all.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
    }
}

minifyJs.finalizedBy(deleteFiles)
deleteFiles.finalizedBy(copyCombinedSources)
bake.finalizedBy(minifyJs)


